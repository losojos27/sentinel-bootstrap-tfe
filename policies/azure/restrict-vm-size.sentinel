# Policy to restrict which "size" that can be used on ["azurerm_virtual_machine", "azurerm_linux_virtual_machine", "azurerm_windows_virtual_machine"] resources

# Import common-functions/tfplan-functions/tfplan-functions.sentinel
# with alias "plan"
import "tfplan-functions" as plan

# List of allowed VM Sizes
param allowed_sizes default ["Standard_A1", "Standard_A2", "Standard_D1_v2", "Standard_D2_v2"]

# Get all Azure VMs
all_azure_vms = plan.find_resources("azurerm_virtual_machine")
all_azure_windows_vms = plan.find_resources("azurerm_windows_virtual_machine")
all_azure_linux_vms = plan.find_resources("azurerm_linux_virtual_machine")

# Filter to Azure VMs with violations
# Warnings will be printed for all violations since the last parameter is true
violating_azure_vms = plan.filter_attribute_not_in_list(all_azure_vms, "vm_size", allowed_sizes, true)
violating_azure_windows_vms = plan.filter_attribute_not_in_list(all_azure_windows_vms, "size", allowed_sizes, true)
violating_azure_linux_vms = plan.filter_attribute_not_in_list(all_azure_linux_vms, "size", allowed_sizes, true)

# Main rule
main = rule {
	length(violating_azure_vms["messages"]) is 0 and
	length(violating_azure_windows_vms["messages"]) is 0 and
	length(violating_azure_linux_vms["messages"]) is 0
}
